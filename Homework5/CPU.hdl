// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: cpsc5020/homework5/Sarah_Parker/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the BOOM output (when BOOM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and BOOM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
	
	Not(in=instruction[15],out=ni);
	
	//ARegister
	Mux16(a=outtoM,b=instruction,sel=ni,out=i);
	Or(a=ni,b=instruction[5],out=intoA);
	ARegister(in=i,load=intoA,out=A,out[0..14]=addressM);


	And(a=instruction[15],b=instruction[12],out=AorM);
	Mux16(a=A,b=inM,sel=AorM,out=AM);
	
	//DRegister
	And(a=instruction[15],b=instruction[4],out=intoD);
	DRegister(in=outtoM,load=intoD,out=Dout);

	//ALU
	ALU(x=Dout,y=AM,zx=instruction[11],nx=instruction[10],zy=instruction[9],ny=instruction[8],f=instruction[7],no=instruction[6],out=outtoM,out=outM,zr=zr,ng=ng);

	
	//Output
	And(a=instruction[15],b=instruction[3],out=writeM);

	Not(in=ng,out=posNum);
	Not(in=zr,out=notZR);
	And(a=instruction[15],b=instruction[0],out=jgt);
	And(a=posNum,b=notZR,out=posNotZR);
	And(a=jgt,b=posNotZR,out=ld1);

	And(a=instruction[15],b=instruction[1],out=jeq);
	And(a=jeq,b=zr,out=ld2);

	And(a=instruction[15],b=instruction[2],out=jlt);
	And(a=jlt,b=ng,out=ld3);

	Or(a=ld1,b=ld2,out=ldt);
	Or(a=ld3,b=ldt,out=ld);

	//PC
	PC(in=A,load=ld,inc=true,reset=reset,out[0..14]=pc);

}


